apply plugin: 'com.android.application'

def Properties keyProps = new Properties()
keyProps.load(project.rootProject.file('signing.properties').newDataInputStream())

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    signingConfigs {
        debug {
            storeFile file(keyProps["debugStoreFile"])
            storePassword keyProps["debugStorePassword"]
            keyAlias keyProps["debugKeyAlias"]
            keyPassword keyProps["debugKeyPassword"]
        }
        release {
            storeFile file(keyProps["releaseStoreFile"])
            storePassword keyProps["releaseStorePassword"]
            keyAlias keyProps["releaseKeyAlias"]
            keyPassword keyProps["releaseKeyPassword"]
        }
    }

    defaultConfig {
        applicationId "net.maiatoday.ristretto"
        minSdkVersion 16
        targetSdkVersion 23
        versionCode System.getenv("BUILD_NUMBER") as Integer ?: 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:23.3.0'
    androidTestCompile ('com.android.support.test:runner:0.3') { exclude module: 'support-annotations' }
    androidTestCompile ('com.android.support.test:rules:0.3') { exclude module: 'support-annotations' }
    androidTestCompile ('com.android.support.test.espresso:espresso-core:2.2') { exclude module: 'support-annotations' }
}
